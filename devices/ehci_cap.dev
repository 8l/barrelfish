/*
 * Copyright (c) 2009, ETH Zurich. All rights reserved.
 *
 * This file is distributed under the terms in the attached LICENSE file.
 * If you do not find this file, copies can be found by writing to:
 * ETH Zurich D-INFK, Haldeneggsteig 4, CH-8092 Zurich. Attn: Systems Group.
 */

/*
 * ehci.dev
 *
 * DESCRIPTION: Enhanced Host Controller Interface description
 *
 * Numbers in comments refer to the Intel EHCI documentation at
 * http://wiki.netos.ethz.ch/Barrelfish/HWInfo
 */

device ehci_cap msbfirst ( addr base) "Intel EHCI capability registers" {

/* Section 2.2 
 * Host controller capabilty registers 
 */

// section 2.2.1 capabilty register length
register cap_length ro addr(base, 0x00) "Capability register length" type(uint8);


// section 2.2.2 Host controller interface version number 
register hci_revision ro addr(base, 0x02) "HCI version number" type(uint16);

// section 2.2.3 Structural parameters
register hcs_params ro addr(base, 0x04) "Structural parameters" {
   _        8   mbz;
   dpn      4   ro   "Debug port number";
   _        3   mbz;
   pi       1   ro   "Port indicator";
   n_cc     4   ro   " Number of campanion controller";
   n_pcc    4   ro   " Number of ports per companion controller";
   prr      1   ro   " Port routing rules";
   _        2   mbz;
   ppc      1   ro   "Port power control";
   n_ports  4   ro   "Number of physical downstream ports on this controller";
   }; 

//section 2.2.4 Capability parameters 
register hcc_params ro addr(base, 0x08) "Capability parameters" {
   _        16 mbz;
   eecp     8  ro   "EHCI extended capabilities pointer"; 
   ist      4  ro   "Isochronous scheduling threshold";
   _        1  mbz;
   aspc     1  ro   "Asynchronous sechedule park capability";
   pflf     1  ro   "Programmable frame list flag";
   ex_ac    1  ro   "Extended 64 bit addressing capability";
 };

// section 2.2.5 Companion port route descriptor 
 //TODO: Fix it  
/*register hcsp_port_route ro addr(base, 0x0C) "Companion port route description" {
  attr     60 ro " ? ";
 };*/

 }; 
